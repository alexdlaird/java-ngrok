plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "jacoco"
    id "idea"
    id "com.adarshr.test-logger" version "4.0.0"
}

group "com.github.alexdlaird"
version "1.4.8"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.yaml:snakeyaml:2.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testImplementation "org.mockito:mockito-core:4.11.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        xml.required = true
        xml.destination file("build/reports/jacoco/report.xml")
    }
}

publishing {
    publications {
        javaLibrary(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = Project.getName()
                description = "A Java wrapper for ngrok"
                url = "http://github.com/alexdlaird/java-ngrok"
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "https://github.com/alexdlaird/java-ngrok/blob/master/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "alexdlaird"
                        name = "Alex Laird"
                        email = "contact@alexlaird.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/alexdlaird/java-ngrok.git"
                    developerConnection = "scm:git:ssh://github.com/alexdlaird/java-ngrok.git"
                    url = "http://github.com/alexdlaird/java-ngrok"
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.javaLibrary
}

javadoc {
    options.overview = "src/main/java/overview.html"
    options.addStringOption('Xdoclint:none', '-quiet')
}
